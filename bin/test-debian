#!/usr/bin/env fish

set -l image "ubuntu:22.04"
set -l repo (pwd)

function info
    if type -q gum
        gum style --border rounded --padding "0 1" $argv
    else
        echo $argv
    end
end

if not type -q docker
    echo "Error: docker is not available on PATH"
    exit 1
end

if not test -f "$repo/bin/dotf"
    echo "Error: run this script from the dotfiles repo root"
    exit 1
end

info "Running dotfiles in $image"
set -l docker_flags --rm
if test -t 0
    set -a docker_flags -it
end

docker run $docker_flags \
    -e CI=true \
    -e NONINTERACTIVE=true \
    -e DEBUG=true \
    -v "$repo:/dotfiles" \
    -w /dotfiles \
    $image \
    bash -lc "./bin/dotf run"
